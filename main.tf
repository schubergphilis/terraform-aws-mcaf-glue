data "aws_region" "current" {}

data "aws_iam_policy_document" "default" {
  statement {
    actions = [
      "sts:AssumeRole"
    ]
    principals {
      type        = "Service"
      identifiers = ["glue.amazonaws.com"]
    }
  }
}

resource "aws_iam_role" "default" {
  count              = var.role_arn == null ? 1 : 0
  name               = "GlueRole-${var.name}"
  assume_role_policy = data.aws_iam_policy_document.default.json
  tags               = var.tags
}

resource "aws_iam_role_policy" "default" {
  count  = var.policy != null ? (var.role_arn != null ? 1 : 0) : 0
  name   = "GlueRolePolicy-${var.name}"
  role   = aws_iam_role.default[0].id
  policy = var.policy
}

resource "aws_iam_role_policy_attachment" "default" {
  count      = var.role_arn == null ? 1 : 0
  role       = aws_iam_role.default[0].id
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSGlueServiceRole"
}

resource "aws_glue_job" "default" {
  name              = var.name
  connections       = var.connections
  default_arguments = var.default_arguments
  glue_version      = var.glue_version
  max_capacity      = var.max_capacity
  max_retries       = var.max_retries
  role_arn          = var.role_arn != null ? var.role_arn : aws_iam_role.default[0].arn
  tags              = var.tags

  command {
    name            = var.command_name
    python_version  = var.python_version
    script_location = var.script_location
  }
}

resource "aws_glue_trigger" "default" {
  name     = var.name
  enabled  = var.trigger_enabled
  schedule = var.trigger_schedule
  type     = var.trigger_type
  tags     = var.tags

  actions {
    job_name = aws_glue_job.default.name
  }

  dynamic "predicate" {
    iterator = predicate
    for_each = var.trigger_predicate
    content {
      logical = lookup(predicate.value, "logical", "AND")
      conditions {
        job_name         = lookup(predicate.value, "job_name", null)
        state            = lookup(predicate.value, "state", null)
        crawler_name     = lookup(predicate.value, "crawler_name", null)
        crawl_state      = lookup(predicate.value, "crawl_state", null)
        logical_operator = lookup(predicate.value, "logical_operator", null)
      }
    }
  }
}
